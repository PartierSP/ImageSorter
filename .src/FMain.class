' Gambas class file

Public Sub Bt_Close_Click()

  Quit

End

Public Sub CB_Move_Click()

  If CB_Move.Value = True Then
    Bt_Doit.Text = "Link"
  Else
    Bt_Doit.Text = "Copy"
  Endif

End

Public Sub DC_Source_Change()

  If DC_Source.Value == DC_Out.Value Then
    Bt_Doit.Enabled = False
  Else
    Bt_Doit.Enabled = True
  Endif
  
  If InStr(DC_Out.Value, DC_Source.Value) == 1 Then
    CB_Cascade.Enabled = False
  Else
    CB_Cascade.Enabled = True
  Endif

End

Public Sub DC_Out_Change()

  If DC_Source.Value == DC_Out.Value Then
    Bt_Doit.Enabled = False
  Else
    Bt_Doit.Enabled = True
  Endif

  If InStr(DC_Out.Value, DC_Source.Value) == 1 Then
    CB_Cascade.Enabled = False
  Else
    CB_Cascade.Enabled = True
  Endif

End

Public Function ImageSort(sDir As String)

  Dim sFiles As String[]
  Dim sFileName As String
  Dim sDate As String
  Dim sYear As String
  Dim sMonth As String
  Dim sDay As String
  Dim sDirName As String
  
  If CB_Cascade.Value == -1 And CB_Cascade.Enabled == True Then
    For Each sDirName In Dir(sDir, "*", gb.Directory)
      Debug sDirName
      ImageSort(sDir &/ sDirName)
    Next
  Endif
  
  sFiles = Dir(sDir, "*.JPG")
  
  For Each SFileName In sFiles
    Debug sFileName
    Shell "exif -t 0x9003 -m " & sDir & "/" & sFileName To sDate
    Debug sDate
    If sDate > "" Then
      sYear = Mid(sDate, 1, 4)
      sMonth = Mid(sDate, 6, 2)
      sDay = Mid(sDate, 9, 2)
    
      Debug sYear & "-" & sMonth & "-" & sDay

      If Exist(DC_Out.Value & "/" & sYear) == False Then
        Mkdir DC_Out.Value & "/" & sYear
      Endif 
      If Exist(DC_Out.Value & "/" & sYear & "/" & sMonth) == False Then
        Mkdir DC_Out.Value & "/" & sYear & "/" & sMonth
      Endif
      If Exist(DC_Out.Value & "/" & sYear & "/" & sMonth & "/" & sDay) == False Then
        Mkdir DC_Out.Value & "/" & sYear & "/" & sMonth & "/" & sDay
      Endif
      If Exist(DC_Out.Value & "/" & sYear & "/" & sMonth & "/" & sDay & "/" & sFileName) == False Then
        If CB_Move.Value = False Then
          Copy sDir & "/" & sFileName To DC_Out.Value & "/" & sYear & "/" & sMonth & "/" & sDay & "/" & sFileName
        Else
          Link sDir & "/" & sFileName To DC_Out.Value & "/" & sYear & "/" & sMonth & "/" & sDay & "/" & sFileName
        Endif
        LCDCopy.Value += 1
        Wait
      Else
        LCDSkip.Value += 1
        Wait
        Debug DC_Out.Value & "/" & sYear & "/" & sMonth & "/" & sDay & "/" & sFileName & " already exists."
      Endif
    Else
      Debug "Error no date found in image " & sFileName
      LCDError.Value += 1
    Endif
  
  Next

End

Public Sub Bt_Doit_Click()

  LCDCopy.Value = 0
  LCDSkip.Value = 0
  LCDError.Value = 0
  
  Bt_Doit.Enabled = False
  Bt_Close.Enabled = False

  ImageSort(DC_Source.Value)
  
  DC_Out.Reload
  DC_Source.Reload
  
  Message.Info("Operation completed.")
  
  Bt_Close.Enabled = True
  Bt_Doit.Enabled = True

End
